#include "zcommon.acs"

//==============================================================================================

//This file has been inspired from the WAD provided by 'basic.cfg' under the ViZDoom repository
// in the python examples section
// See here: https://github.com/Farama-Foundation/ViZDoom/blob/master/scenarios/basic.cfg
//
//To view the ACS file however, you need to open the WAD file in some map viewer application
// like this one.

//==============================================================================================

//Brief Summary:

	// - Ensures agent has only access to the gun
	// - Provides negative reward for every shot taken by agent
	// - Randomly spawns the enemies each episode
	// - Provides positive reward for killing Cacodemons
	// - Provides Negative rewards for shooting a bullet through function 3
	// - Provides negative rewards for killing wrong enemy types (i.e. not Cacodemons) through function 5
	//
	// - Episode finishes once all Cacodemon enemies have been killed. This has been set
	//   using function 4
	//
	// - Uses a specialised function to randomly spawn enemies in the map, retrying on failure.
	//   This utilises thing type MapSpot, where a random MapSpot is chosen for enemy placement

//=====================================================================

////============================Main Script============================

//Arbitrary values set for enemy TIDs (Target ID). Simply add more for more enemies where needed
// This scenario uses:

	// 20 = Cacodemon_1
	// 21 = Cacodemon_2
	// 22 = Pain Elemental_1
	// 23 = Pain Elemental_2
	// 24 = Lost Soul_1
	// 25 = Lost Soul_2
	// 26 = Lost Soul_3
	// 27 = Cyberdemon_1
	// 28 = Cyberdemon_2
	// 29 = Zombieman

int target_id_1 = 20;
int target_id_2 = 21;
int target_id_3 = 22;
int target_id_4 = 23;
int target_id_5 = 24;
int target_id_6 = 25;
int target_id_7 = 26;
int target_id_8 = 27;
int target_id_9 = 28;

//number of target enemies (Cacodemons) to kill
int total_targets_to_kill = 1;

global int 1: reward;
global int 2: total_bullets_available;
global int 3: bullets_used;

int tic_count = 0;


script 1 OPEN
{
    SpawnTarget(target_id_1, "Cacodemon", 100, 140);

	SpawnTarget(target_id_3, "PainElemental", 100, 140);
	SpawnTarget(target_id_4, "PainElemental", 100, 140);

	SpawnTarget(target_id_5, "LostSoul", 100, 140);
	SpawnTarget(target_id_6, "LostSoul", 100, 140);
	SpawnTarget(target_id_6, "LostSoul", 100, 140);

	SpawnTarget(target_id_7, "Cyberdemon", 100, 140);
	SpawnTarget(target_id_8, "Cyberdemon", 100, 140);

	SpawnTarget(target_id_9, "ZombieMan", 100, 140);


    reward = 0;
	bullets_used = 0;

}


script "LivingReward" ENTER {

	tic_count = 0;

	while(TRUE){

		tic_count++;

		if (tic_count % (35) == 0){
			reward -= 10;
		}

		Delay(1);
	}
}


script 2 ENTER
{
    TakeInventory("Fist",1);
    ACS_Execute(3, 1, 0, 0, 0);

	total_bullets_available = CheckInventory("Clip");
	//print(s: "Total bullets: ", d: total_bullets_available);

}

script 3 ENTER{

    int bullets = CheckInventory("Clip");

	//This way the game handles this aspect using proper game closure methods
    while(true){

        int t_bullets = CheckInventory("Clip");
		int bullets_delta = bullets - t_bullets;



		//Makes sure that the difference is calculated exactly, rather than
		// just per tic as that could cause an outdated value to be retained

		reward -= 100 * bullets_delta;
		bullets_used += bullets_delta;

		//print(s: "Bullet shot, bullets used = \n", d: bullets_used, s: "Reward: ", d: reward);


        bullets = t_bullets;
        delay(1);
    }
}


script 4 DEATH {

	if (GetActorProperty(0, APROP_Health) == 100){

		//print(s: "Target has been killed");

		//print(d: GetActorProperty(0, APROP_Health));


		total_targets_to_kill -= 1;

		if (total_targets_to_kill == 0) {

			reward += 15000;

			//reward -= 1;

			//print(s: "Total reward: ", d: reward, s: "\n Bullets at Start = ", d: total_bullets_available, s: " Bullets Used: ", d: bullets_used + 1);
			//delay(200);
			Exit_Normal(0);
		}
	}

}

script 5 DEATH{

	if (GetActorProperty(0, APROP_Health) == 100){

		//print(s: "Wrong Target has been killed");

		//print(d: GetActorProperty(0, APROP_Health));

		reward += -10000;

	}

}



function void SpawnTarget(int target_tid, str thing_name, int spot_min, int spot_max){

    int spot_tid = Random(spot_min, spot_max);
	int successful_spawn = SpawnSpot(thing_name, spot_tid, target_tid);

	while (!successful_spawn){

		//print(s:"FAILED SpawnSpot for: ", s: thing_name, s: " at spot TID: ", d: spot_tid, s: "\nTrying again...");

		spot_tid = Random(spot_min, spot_max);
		successful_spawn = SpawnSpot(thing_name, spot_tid, target_tid);

	}


    //disables movement
    SetActorProperty(target_tid, APROP_Speed, 0);

    //makes it die on one hit
    SetActorProperty(target_tid, APROP_Health, 1);

    //makes it ignore the player and attack actor with tid 100
    Thing_Hate (target_tid, 100, 6);


	// <22 means cacodemon so use special logic to detriment target enemy counter and end game logic
		// i.e. after cacodemons die, end game
		// if not cacodemon, deduct negative reward

	if (target_tid < 22){
		SetThingSpecial(target_tid, ACS_Execute, 4);

	} else {
		SetThingSpecial(target_tid, ACS_Execute, 5);
	}



	SetActorProperty(target_tid, APROP_Dormant, 1);

}


//=====================================================================