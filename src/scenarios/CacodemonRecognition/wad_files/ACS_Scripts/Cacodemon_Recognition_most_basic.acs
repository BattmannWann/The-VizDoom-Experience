#include "zcommon.acs"

//==============================================================================================

//This file has been inspired from the WAD provided by 'basic.cfg' under the ViZDoom repository
// in the python examples section
// See here: https://github.com/Farama-Foundation/ViZDoom/blob/master/scenarios/basic.cfg
//
//To view the ACS file however, you need to open the WAD file in some map viewer application
// like this one.

//==============================================================================================

//Brief Summary:

	// - Ensures agent has only access to the gun
	// - Provides negative reward for every shot taken by agent
	// - Randomly spawns the enemies each episode
	// - Provides positive reward for killing Cacodemons
	// - Provides Negative rewards for shooting a bullet (insight for agent to use ammo sparingly)
	//
	// - Episode finishes once all Cacodemon enemies have been killed. This has been set
	//   using function 4
	//
	// - Uses a specialised function to randomly spawn enemies in the map, retrying on failure.
	//   This utilises thing type MapSpot, where a random MapSpot is chosen for enemy placement

//=====================================================================

////============================Main Script============================

//Arbitrary values set for enemy TIDs (Target ID). Simply add more for more enemies where needed
// This scenario uses:

	// 20 = Cacodemon_1
	// 21 = Cacodemon_2


int target_id_1 = 20;
int target_id_2 = 21;


//number of target enemies (Cacodemons) to kill
int total_targets_to_kill = 2;

global int 0: reward;
global int 1: total_bullets_available;
global int 2: bullets_used;


script 1 OPEN
{
    SpawnTarget(target_id_1, "Cacodemon", 97, 112);
	SpawnTarget(target_id_2, "Cacodemon", 97, 112);


    reward = 0;
	bullets_used = 0;

}


script 2 ENTER
{
    TakeInventory("Fist",1);
    ACS_Execute(3, 1, 0, 0, 0);

	total_bullets_available = CheckInventory("Clip");
	//print(s: "Total bullets: ", d: total_bullets_available);

}

script 3 (void)
{
    int bullets = CheckInventory("Clip");

    while(true)
    {
        int t_bullets = CheckInventory("Clip");

        if(t_bullets < bullets)
        {
            reward = reward - 5;
			bullets_used += bullets - t_bullets;
        }

        bullets = t_bullets;
        delay(1);
    }
}

script 4 (void)
{
    reward = reward + 106;
	//print(s: "Killed");
	total_targets_to_kill -= 1;

	if (total_targets_to_kill == 0) {
		//print(s: "Total reward: ", d: reward, s: "\n Bullets at Start = ", d: total_bullets_available, s: " Bullets Used: ", d: bullets_used + 1);
		//delay(20);
		Exit_Normal(0);
	}


}




function void SpawnTarget(int target_tid, str thing_name, int spot_min, int spot_max){

    int spot_tid = Random(spot_min, spot_max);
	int successful_spawn = SpawnSpot(thing_name, spot_tid, target_tid);

	while (!successful_spawn){

		//print(s:"FAILED SpawnSpot for: ", s: thing_name, s: " at spot TID: ", d: spot_tid, s: "\nTrying again...");

		spot_tid = Random(spot_min, spot_max);
		successful_spawn = SpawnSpot(thing_name, spot_tid, target_tid);

	}


    //disables movement
    SetActorProperty(target_tid, APROP_Speed, 0);

    //makes it die on one hit
    SetActorProperty(target_tid, APROP_Health, 1);

    //makes it ignore the player and attack actor with tid 100
    Thing_Hate (target_tid, 100, 6);


	// <22 means cacodemon so use special logic to detriment target enemy counter and end game logic
		// i.e. after cacodemons die, end game
		// if not cacodemon, deduct negative reward


	SetThingSpecial(target_tid, ACS_ExecuteAlways, 4);



	SetActorProperty(target_tid, APROP_Dormant, 1);

}


//=====================================================================